# -------------------------------------------------------------------------
# INFLECTION
# -------------------------------------------------------------------------

PATTERNS
NounAbs
NounAbsPl
NounObl[m] CasesM
NounOblPl[m] Case[-f]
NounObl[-m] Case[-m]
NounObl[-m] Case[-m,gen] Plural?
NounOblPl[-m] Case[-m]
NounOblPl[-m] Case[-m,gen] Plural?

NumeralsAbs OblStem[m] CasesM
NumeralsAbs OblStem[-m] (Case[-v_epentetic,-m] | (Epent[sg] Case[v_epentetic,-m]))
NumeralsAbs OblStem[-m] Epent[sg] Case[-m,gen] Plural?                          ## plural forms of genetive

AdjectiveMSg (OblStem[m] CasesM)?
AdjectiveMPl Plural (Epent[m] Case[-f])?
AdjectiveSgConsonantal (Case[-v_epentetic,-m] | (Epent[sg] Case[v_epentetic,-m]))?
AdjectiveSgConsonantal Epent[sg] Case[-m,gen] Plural?                  ## plural forms of genetive

AdjectiveSgVocalic (OblStem[-m] (Case[-v_epentetic,-m] | (Epent[sg] Case[v_epentetic,-m])))?
AdjectivePl Plural (Epent[pl] Case[-m])?
AdjectivePl Plural Epent[pl] Case[-m,gen] Plural?                      ## plural forms of genetive

PATTERN AdjectiveMSg
Adjectives[-initial_aggr,-final_aggr]
(agrCL[m] Adjectives[initial_aggr,-plural_slot])
(Adjectives[final_aggr] agrCL[m])
agrCL[-pl,m] [:у] Adjectives[initial_aggr,plural_slot]

Adverbs[-initial_aggr,-final_aggr] [<ADV>:] Attributivizer
agrCL[m] Adverbs[initial_aggr] [<ADV>:] Attributivizer
Adverbs[final_aggr] agrCL[m] [<ADV>:] Attributivizer


PATTERN AdjectiveMPl
Adjectives[-initial_aggr,-final_aggr]
agrCL[m] Adjectives[initial_aggr,-plural_slot]
Adjectives[final_aggr] agrCL[m]
agrCL[-sg,m] [:о] Adjectives[initial_aggr,plural_slot]

Adverbs[-initial_aggr,-final_aggr] [<ADV>:] Attributivizer
agrCL[m] Adverbs[initial_aggr] [<ADV>:] Attributivizer
Adverbs[final_aggr] agrCL[m] [<ADV>:] Attributivizer

PATTERN AdjectiveSgConsonantal
Adjectives[-initial_aggr,-final_aggr,сonsonantal]
agrCL[-m,-pl] Adjectives[initial_aggr,сonsonantal,-plural_slot]
Adjectives[final_aggr] agrCL[-m,-pl]
agrCL[-m,-pl] [:и] Adjectives[initial_aggr,plural_slot,сonsonantal]

PATTERN AdjectiveSgVocalic
Adjectives[-initial_aggr,-final_aggr,-сonsonantal]
agrCL[-m,-pl] Adjectives[initial_aggr,-сonsonantal,-plural_slot]
agrCL[-m,-pl] [:и] Adjectives[initial_aggr,plural_slot,-сonsonantal]

Adverbs[-initial_aggr,-final_aggr] [<ADV>:] Attributivizer
agrCL[-m,-pl] Adverbs[initial_aggr] [<ADV>:] Attributivizer
Adverbs[final_aggr] agrCL[-m,-pl] [<ADV>:] Attributivizer


PATTERN AdjectivePl
Adjectives[-initial_aggr,-final_aggr]
agrCL[-m,-sg] Adjectives[initial_aggr,-plural_slot]
Adjectives[final_aggr] agrCL[-m,-sg]
agrCL[-m,-sg] [:о] Adjectives[initial_aggr,plural_slot,-сonsonantal]

Adverbs[-initial_aggr,-final_aggr] [<ADV>:] Attributivizer
agrCL[-m,-sg] Adverbs[initial_aggr] [<ADV>:] Attributivizer
Adverbs[final_aggr] agrCL[-m,-sg] [<ADV>:] Attributivizer

## Add inflected noun forms with Attributivizer
## Add inflected verb forms with Attributivizer

LEXICON Epent
<epent.m>:>у[m]
<epent.sg>:>лъи[sg]
<epent.pl>:>и[pl]

LEXICON OblStem
<obl.m>:>ш[m]
<obl>:>л

PATTERN CasesM
Epent[m] Case[v_epentetic,-f]
Epent[m] Case[gen] Plural?
Case[-v_epentetic,-f]
Case[ess] Elative

LEXICON Case
<erg>:>ди
<dat>:>лъу[f]
<dat>:>й[v_epentetic]
<gen>:>лIи[f,gen]
<m><gen>:>в[m,v_epentetic,gen]
<f><gen>:>й[m,v_epentetic,gen]
<inan1><gen>:>б[m,v_epentetic,gen]
<inan2><gen>:>р[m,v_epentetic,gen]
<an.sg><gen>:>б[m,v_epentetic,gen]
<an.pl><gen>:>й[m,v_epentetic,gen]

<m><aff>:>во[v_epentetic]
<f><aff>:>йо[v_epentetic]
<inan1><aff>:>бо[v_epentetic]
<inan2><aff>:>ро[v_epentetic]
<an.sg><aff>:>бо[v_epentetic]
<an.pl><aff>:>йо[v_epentetic]

<sub><ess>:>кьи[ess]
<sub><lat>:>кьи
<super><ess>:>ъа[ess]
<super><lat>:>ъо
<in><ess>:>ла[ess]
<super><lat>:>ди
<inter><ess>:>лIи[ess]
<inter><lat>:>лIи
<add><ess>:>ха[ess]
<add><lat>:>хо
<apud><ess>:>хъи[ess]
<apud><lat>:>хъи
<cont><ess>:>чIу[ess]
<contr><lat>:>чIу

LEXICON Elative
<elat>:>кку

LEXICON Attributivizer
<attr>:>сси

LEXICON Plural
<pl>:>{adj.pl}л

