# -------------------------------------------------------------------------
# ADJECTIVES
# -------------------------------------------------------------------------

PATTERNS
AdjectivesAbs

PATTERN AdjectivesAbs
Adjectives[-initial_aggr,-final_aggr] [<ADJ>:] AdjectivePlural?

agrCL Adjectives[initial_aggr] [<ADJ>:]
agrCL[-sg] Adjectives[initial_aggr] [<ADJ>:] AdjectivePlural

Adjectives[final_aggr] agrCL [<ADJ>:]
Adjectives[final_aggr] agrCL[-sg] [<ADJ>:] AdjectivePlural

PATTERN AdjectivesObl
Adjectives[-initial_aggr,-final_aggr,-consonantal] [<ADJ>:]

LEXICON AdjectivePlural
<pl>:>{adj.pl}л

LEXICON Adjectives
шу[final_aggr,сonsonantal]                                                      # хороший; good
охор[initial_aggr,сonsonantal]                                                  # старый; old
ерцIцIом[initial_aggr,сonsonantal]                                              # чистый; clean
ечIуха[initial_aggr]                                                            # большой; clean
очо[initial_aggr]                                                               # низкий; short
ехула[initial_aggr]                                                             # высокий; tall
иши[initial_aggr]                                                               # толстый; fat
ечIедир[initial_aggr,сonsonantal]                                               # черный; black
ехула[initial_aggr]                                                             # длинный; long
чуруки                                                                          # грязный; dirty
уди                                                                             # глубокий; deep
ъаб[сonsonantal]                                                                # широкий; wide
гьацIа                                                                          # белый; white
чакку                                                                           # желтый; yellow
хвечи                                                                           # зеленый; green
цIиᵸву                                                                          # новый; new
ботIи                                                                           # слепой; blind
чIчIитIир[сonsonantal]                                                          # узкий; narrow
лъему                                                                           # жидкий; liquid
цIцIикIкIу                                                                      # кислый; sour 
чIило                                                                           # кривой; curved
гъвенчIил[сonsonantal]                                                          # кривой; curved
кучIил[сonsonantal]                                                             # левый; left
олохъан[сonsonantal]                                                            # молодой; young
кьухъур[сonsonantal]                                                            # низкий; low
уᵸнса                                                                           # тёплый; warm 
рекъу                                                                           # хромой; lame
гьири                                                                           # красный; red
бечIугу                                                                         # пустой; empty
хъелла                                                                          # худой; thin
бискин                                                                          # бедный; poor


# -------------------------------------------------------------------------
# ADVERBS
# -------------------------------------------------------------------------

PATTERNS
Adverbs[no_aggr] [<ADV>:]
agrCL Adverbs[initial_aggr] [<ADV>:]
Adverbs[final_aggr] agrCL [<ADV>:]

LEXICON Adverbs
суни[no_aggr]                                                                   # вчера; yesterday
золо[no_aggr]                                                                   # очень; very 
тIулу[no_aggr]                                                                  # очень; very
желъал[no_aggr]                                                                 # сегодня; today
жегьал[no_aggr]                                                                 # сегодня; today
гьикьу[no_aggr]                                                                 # внизу; at the bottom
сседу[no_aggr]                                                                  # впереди; in front of
гьонолъу[no_aggr]                                                               # здесь; here
игьолгу[initial_aggr]                                                           # много; a lot of
игьугу[initial_aggr]                                                            # много; many

# -------------------------------------------------------------------------
# AGREEMENT
# -------------------------------------------------------------------------

LEXICON agrCL
<m>:в[m]
<f>:й
<inan1>:б
<inan2>:р
<an.sg>:б[sg]
<an.pl>:й[pl]

# -------------------------------------------------------------------------
# CLITICS
# -------------------------------------------------------------------------

LEXICON clitics
<wh>:>ди
<wh>:>кIо
<wh>:>гъи
<q>:>диле
<q>:>кIоле
<q>:>ле
<q.pst>:>гъиро
<rep>:>лъоди
<rep>:>гъоди
<quot>:>гъоду
<quot>:>ду
<int>:>гу
<advz>:>гужа
<add>:>ло
<coll>:>ло
<com>:>лой
<equ>:>лъий
<sbd>:>лоду
<futimp>:>хъо
<attr>:>сси

# -------------------------------------------------------------------------
# INFLECTION
# -------------------------------------------------------------------------

PATTERNS
NumeralsAbs OblStem[m] (([:у] Case[v_epentetic]) | Case[-v_epentetic] | (LocCase Elative?))
NumeralsAbs OblStem[-m,-сonsonantal] (([:у] Case[-m,v_epentetic]) | Case[-m,-v_epentetic] | (LocCase Elative?))

Adjectives[-initial_aggr,-final_aggr] [<ADJ>:] OblStem[m] (([:у] Case[v_epentetic]) | Case[-v_epentetic] | (LocCase Elative?))
Adjectives[-initial_aggr,-final_aggr,consonantal] [<ADJ>:] OblStem[consonantal] (Case[-m] | (LocCase Elative?))
Adjectives[-initial_aggr,-final_aggr,-consonantal] [<ADJ>:] OblStem[-consonantal] (([:у] Case[-m,v_epentetic]) | Case[-m,-v_epentetic] | (LocCase Elative?))
Adjectives[final_aggr] agrCL[m] [<ADJ>:] OblStem[m] (([:у] Case[v_epentetic]) | Case[-v_epentetic] | (LocCase Elative?))
Adjectives[final_aggr] agrCL[-m] [<ADJ>:] OblStem[consonantal] (Case[-m] | (LocCase Elative?))
agrCL[m] Adjectives[initial_aggr] [<ADJ>:] OblStem[m] (([:у] Case[v_epentetic]) | Case[-v_epentetic] | (LocCase Elative?))
agrCL[-m] Adjectives[initial_aggr,consonantal] [<ADJ>:] OblStem[consonantal] (Case[-m] | (LocCase Elative?))
agrCL[-m] Adjectives[initial_aggr,-consonantal] [<ADJ>:] OblStem[-consonantal] (Case[-m] | (LocCase Elative?))

LEXICON OblStem
<obl.m>:>ш[m]
<obl>:>л
<obl>:>лъи[сonsonantal]

LEXICON Case
<erg>:>ди
<dat>:>лъу
<gen>:>лIи
<dat>:>й[v_epentetic]
<m><gen>:>в[m,v_epentetic]
<f><gen>:>й[m,v_epentetic]
<inan1><gen>:>б[m,v_epentetic]
<inan2><gen>:>р[m,v_epentetic]
<an.sg><gen>:б[m,sg,v_epentetic]
<an.pl><gen>:й[m,pl,v_epentetic]

<m><aff>:>во[v_epentetic]
<f><aff>:>йо[v_epentetic]
<inan1><aff>:>бо[v_epentetic]
<inan2><aff>:>ро[v_epentetic]
<an.sg><aff>:бо[sg,v_epentetic]
<an.pl><aff>:йо[pl,v_epentetic]

LEXICON Elative
<elat>:кку

LEXICON LocCase
<sub><ess>:>кьи[ess]
<sub><lat>:>кьи
<super><ess>:>ъа[ess]
<super><lat>:>ъо
<in><ess>:>ла[ess]
<super><lat>:>ди
<inter><ess>:>лIи[ess]
<inter><lat>:>лIи
<add><ess>:>ха[ess]
<add><lat>:>хо
<apud><ess>:>хъи[ess]
<apud><lat>:>хъи
<cont><ess>:>чIу[ess]
<contr><lat>:>чIу
# -------------------------------------------------------------------------
# NUMERALS
# -------------------------------------------------------------------------

PATTERNS
NumeralsAbs
NumeralsNotAbs

PATTERN NumeralsAbs
Numerals[one] [<NUM>:] agrCL[-pl]                                               ## 1
(Numerals[-one,-four] | NumeralFour) [<NUM>:] NumearalMarker                    ## rest numerals
NumeralMultiplier (NumeralCrown | (Numerals[base] [<NUM>:])) NumearalMarker     ## crowns, 100, 1000

PATTERN NumeralsNotAbs
Numerals[ten] [<NUM>:] NumearalTeenLinker                                       ## ten in 11-19
NumeralMultiplier NumeralCrown NumearalLinker                                   ## crowns
NumeralMultiplier? Numerals[base] [<NUM>:]                                      ## 100, 1000

PATTERN NumeralMultiplier
(Numerals[multiplier] | NumeralFour) [<NUM>:]

PATTERN NumeralFour
agrCL[-pl] Numerals[four]

LEXICON Numerals
се[one]                                                                         # один; one
чIе[multiplier]                                                                 # два; two
лъоб[multiplier]                                                                # три; three
оъо[four]                                                                       # четыре; four
иᵸшду[multiplier]                                                               # пять; five
ойлIи[multiplier]                                                               # шесть; six
гьокьу[multiplier]                                                              # семь; seven
бейкьи[multiplier]                                                              # восемь; eight
гьочIо[multiplier]                                                              # девять; nine
гьоцIо[ten]                                                                     # десять; ten
бешену:>бешену[base]                                                            # сто; hundred
озуру:>озуру[base]                                                              # тысяча; thousand

LEXICON NumearalMarker
<num>:>гу

LEXICON NumearalTeenLinker
<teen_link>:>лъий

LEXICON NumearalLinker
<linker>:>лой

LEXICON NumeralCrown
<crown>:>цIол

