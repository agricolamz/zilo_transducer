# -------------------------------------------------------------------------
# ADJECTIVES
# -------------------------------------------------------------------------

PATTERNS
Adjectives[no_aggr] [<ADJ>:] AdjectivePlural?

agrCL Adjectives[initial_aggr] [<ADJ>:]
agrCL[-sg] Adjectives[initial_aggr] [<ADJ>:] AdjectivePlural

Adjectives[final_aggr] agrCL [<ADJ>:]
Adjectives[final_aggr] agrCL[-sg] [<ADJ>:] AdjectivePlural

LEXICON AdjectivePlural
<pl>:>{adj.pl}ол

LEXICON Adjectives
шу[final_aggr]                                                                  # хороший; good
ехула[initial_aggr]                                                             # высокий; tall
иши[initial_aggr]                                                               # толстый; fat
ечIедир[initial_aggr]                                                           # черный; black
ехула[initial_aggr]                                                             # длинный; long
чуруки[no_aggr]                                                                 # грязный; dirty
уди[no_aggr]                                                                    # глубокий; deep
ъаб[no_aggr]                                                                    # широкий; wide
гьацIа[no_aggr]                                                                 # белый; white
чакку[no_aggr]                                                                  # желтый; yellow
хвечи[no_aggr]                                                                  # зеленый; green
цIиᵸву[no_aggr]                                                                 # новый; new
ботIи[no_aggr]                                                                  # слепой; blind
чIчIитIир[no_aggr]                                                              # узкий; narrow
лъему[no_aggr]                                                                  # жидкий; liquid
цIцIикIкIу[no_aggr]                                                             # кислый; sour 
чIило[no_aggr]                                                                  # кривой; curved
гъвенчIил[no_aggr]                                                              # кривой; curved

# -------------------------------------------------------------------------
# ADVERBS
# -------------------------------------------------------------------------

PATTERNS
Adverbs[no_aggr] [<ADV>:]
agrCL Adverbs[initial_aggr] [<ADV>:]
Adverbs[final_aggr] agrCL [<ADV>:]

LEXICON Adverbs
суни[no_aggr]                                                                   # вчера; yesterday
золо[no_aggr]                                                                   # очень; very 
тIулу[no_aggr]                                                                  # очень; very
желъал[no_aggr]                                                                 # сегодня; today
жегьал[no_aggr]                                                                 # сегодня; today
гьикьу[no_aggr]                                                                 # внизу
сседу[no_aggr]                                                                  # впереди
гьонолъу[no_aggr]                                                               # здесь

# -------------------------------------------------------------------------
# AGREEMENT
# -------------------------------------------------------------------------

LEXICON agrCL
<m>:в
<f>:й
<inan1>:б
<inan2>:р
<an.sg>:б[sg]
<an.pl>:й[pl]

# -------------------------------------------------------------------------
# CLITICS
# -------------------------------------------------------------------------

LEXICON clitics
<wh>:>ди
<wh>:>кIо
<wh>:>гъи
<q>:>диле
<q>:>кIоле
<q>:>ле
<q.pst>:>гъиро
<rep>:>лъоди
<rep>:>гъоди
<quot>:>гъоду
<quot>:>ду
<int>:>гу
<advz>:>гужа
<add>:>ло
<coll>:>ло
<com>:>лой
<equ>:>лъий
<sbd>:>лоду
<futimp>:>хъо
<attr>:>сси

# -------------------------------------------------------------------------
# INFLECTION
# -------------------------------------------------------------------------

PATTERNS
NumeralsAbs OblStem[m,u_epentetic] Case[u_epentetic]
NumeralsAbs OblStem[m,-u_epentetic] Case[-u_epentetic]
NumeralsAbs OblStem[nm,u_epentetic] Case[u_epentetic]
NumeralsAbs OblStem[nm,-u_epentetic] Case[-u_epentetic]

LEXICON OblStem
<obl.m>:>ш[m]
<obl.m>:>шу[m,u_epentetic]
<obl>:>л[nm]

LEXICON Case
<erg>:>ди
<dat>:>й[u_epentetic]
<dat>:>лъу
<m><gen>:>в[m,u_epentetic]
<f><gen>:>й[m,u_epentetic]
<inan1><gen>:>б[m,u_epentetic]
<inan2><gen>:>р[m,u_epentetic]
<an><sg><gen>:б[m,sg,u_epentetic]
<an><pl><gen>:й[m,pl,u_epentetic]
<m><aff>:>во[u_epentetic]
<f><aff>:>йо[u_epentetic]
<inan1><aff>:>бо[u_epentetic]
<inan2><aff>:>ро[u_epentetic]
<an.sg><aff>:бо[sg,u_epentetic]
<an.pl><aff>:йо[pl,u_epentetic]

# -------------------------------------------------------------------------
# NUMERALS
# -------------------------------------------------------------------------

PATTERNS
NumeralsAbs
NumeralsNotAbs

PATTERN NumeralsAbs
Numerals[one] [<NUM>:] agrCL[-pl]                                               ## 1
(Numerals[-one,-four] | NumeralFour) [<NUM>:] NumearalMarker                    ## rest numerals
NumeralMultiplier (NumeralCrown | (Numerals[base] [<NUM>:])) NumearalMarker     ## crowns, 100, 1000

PATTERN NumeralsNotAbs
Numerals[ten] [<NUM>:] NumearalTeenLinker                                       ## ten in 11-19
NumeralMultiplier NumeralCrown NumearalLinker                                   ## crowns
NumeralMultiplier? Numerals[base] [<NUM>:]                                      ## 100, 1000

PATTERN NumeralMultiplier
(Numerals[multiplier] | NumeralFour) [<NUM>:]

PATTERN NumeralFour
agrCL[-pl] Numerals[four]

LEXICON Numerals
се[one]                                                                         # один; one
чIе[multiplier]                                                                 # два; two
лъоб[multiplier]                                                                # три; three
оъо[four]                                                                       # четыре; four
иᵸшду[multiplier]                                                               # пять; five
ойлIи[multiplier]                                                               # шесть; six
гьокьу[multiplier]                                                              # семь; seven
бейкьи[multiplier]                                                              # восемь; eight
гьочIо[multiplier]                                                              # девять; nine
гьоцIо[ten]                                                                     # десять; ten
бешену:>бешену[base]                                                            # сто; hundred
озуру:>озуру[base]                                                              # тысяча; thousand

LEXICON NumearalMarker
<num>:>гу

LEXICON NumearalTeenLinker
<teen_link>:>лъий

LEXICON NumearalLinker
<linker>:>лой

LEXICON NumeralCrown
<crown>:>цIол

