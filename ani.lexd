# NUMERAL PATTERNS

PATTERNS
Numerals[one] [<num>:] agrCL[-PL]                                               # 1
(Numerals[-one,-four] | NumeralFour) [<num>:] NumearalCard                      # rest numerals
Numerals[ten] [<num>:] NumearalTeenLinker                                       # ten in 11-19
# crowns
(Numerals[multiplier] | NumeralFour) [<num>:] NumeralCrown (NumearalCard | NumearalLinker)
# hundreds, thousands
((Numerals[multiplier] | NumeralFour) [<num>:])? Numerals[base] [<num>:] NumearalCard?

PATTERN NumeralFour
agrCL[-PL] Numerals[four]

LEXICON Numerals
се[one]                                                                         # one
чIе[multiplier]                                                                 # two
лъоб[multiplier]                                                                # three
оъо[four]                                                                       # four
иᵸшду[multiplier]                                                               # five
ойлIи[multiplier]                                                               # six
гьокьу[multiplier]                                                              # seven
бейкьи[multiplier]                                                              # eight
гьочIо[multiplier]                                                              # nine
гьоцIо[ten]                                                                     # ten
бешену:>бешену[base]                                                            # hundred
озуру:>озуру[base]                                                              # thousand

LEXICON NumearalCard
<card>:>гу

LEXICON NumearalTeenLinker
<teen_link>:>лъий

LEXICON NumearalLinker
<linker>:>лой

LEXICON NumeralCrown
<crown>:>цIол

LEXICON agrCL
<m>:в
<f>:й
<inan1>:б
<inan2>:р
<an><sg>:б
<an><pl>:й[PL]
