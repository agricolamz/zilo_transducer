# -------------------------------------------------------------------------
# ADJECTIVES
# -------------------------------------------------------------------------

LEXICON AdjectivePlural
<pl>:>{adj.pl}л

LEXICON Adjectives
шу[final_aggr]                                                                  # хороший; good
зарху[final_aggr]                                                               # холодный; cold
екъуб[final_aggr]                                                               # густой; dense 
охор[initial_aggr,сonsonantal]                                                  # старый; old
ерцIцIом[initial_aggr,сonsonantal]                                              # чистый; clean
ечIуха[initial_aggr]                                                            # большой; clean
очо[initial_aggr]                                                               # низкий; short
ехула[initial_aggr]                                                             # высокий; tall
иши[initial_aggr]                                                               # толстый; fat
ечIедир[initial_aggr,сonsonantal]                                               # черный; black
ехула[initial_aggr]                                                             # длинный; long
цIцIиб[initial_aggr,plural_slot]                                                # полный; full, stout
тIонниб[initial_aggr,plural_slot]                                               # мокрый; wet
чIой[initial_aggr,plural_slot]                                                  # мертвый; dead
чуруки                                                                          # грязный; dirty
уди                                                                             # глубокий; deep
ъаб[сonsonantal]                                                                # широкий; wide
гьацIа                                                                          # белый; white
чакку                                                                           # желтый; yellow
хвечи                                                                           # зеленый; green
цIиᵸву                                                                          # новый; new
ботIи                                                                           # слепой; blind
чIчIитIир[сonsonantal]                                                          # узкий; narrow
лъему                                                                           # жидкий; liquid
цIцIикIкIу                                                                      # кислый; sour 
чIило                                                                           # кривой; curved
гъвенчIил[сonsonantal]                                                          # кривой; curved
кучIил[сonsonantal]                                                             # левый; left
олохъан[сonsonantal]                                                            # молодой; young
кьухъур[сonsonantal]                                                            # низкий; low
уᵸнса                                                                           # тёплый; warm 
рекъу                                                                           # хромой; lame
гьири                                                                           # красный; red
бечIугу                                                                         # пустой; empty
хъелла                                                                          # худой; thin
бискин[сonsonantal]                                                             # бедный; poor
гьанъо                                                                          # беременная; pregnant
тохи                                                                            # глупый; stupid
гьанчIчIил[сonsonantal]                                                         # правый; right
хъвей[сonsonantal]                                                              # серый; grey
гургума                                                                         # круглый; round
букьин[сonsonantal]                                                             # горький; bitter 
гьокIору                                                                        # тяжелый; heavy  
къваку                                                                          # сильный; strong 
мицIа                                                                           # сладкий; sweet 
чIантIи                                                                         # мелкий; small
лъолколъи                                                                       # легкий; light
багъу                                                                           # красивый; beautiful
уцIцIилойсси                                                                    # аккуратный; careful
мичIи                                                                           # маленький; small
содо                                                                            # холодный; cold
мицIа                                                                           # сладкий; sweet
джиб[сonsonantal]                                                               # каждый; every
сагъи                                                                           # здоровый; healthy
чIачIи                                                                          # жирный; fatty
чора                                                                            # жирный; fat

# жи=б=улгу                                                                       # все; all
# се=в=ло                                                                         # другой; other



# -------------------------------------------------------------------------
# ADVERBS
# -------------------------------------------------------------------------

PATTERNS
Adverbs[-initial_aggr,-final_aggr] [<ADV>:]
agrCL Adverbs[initial_aggr] [<ADV>:]
Adverbs[final_aggr] agrCL [<ADV>:]

LEXICON Adverbs
суни                                                                            # вчера; yesterday
золо                                                                            # очень; very 
тIулу                                                                           # очень; very
желъал                                                                          # сегодня; today
жегьал                                                                          # сегодня; today
гьикьу                                                                          # внизу; at the bottom
сседу                                                                           # впереди; in front of
гьонолъу                                                                        # здесь; here
игьолгу[initial_aggr]                                                           # много; a lot of
игьугу[initial_aggr]                                                            # много; many
сой                                                                             # послезавтра; day after tomorrow
илIохъи                                                                         # медленно; slowly     
илIогва                                                                         # медленно; slowly
элолла                                                                          # далеко; far away
гьину                                                                           # внутри; inside
хехи                                                                            # быстро; quickly
гьуду                                                                           # там; there
щела                                                                            # завтра; tomorrow
иннагуло                                                                        # иногда; sometimes
гьуᵸ                                                                            # да; yes
джи                                                                             # да; yes

# -------------------------------------------------------------------------
# AGREEMENT
# -------------------------------------------------------------------------

LEXICON agrCL
<m>:в[m]
<f>:й
<inan1>:б
<inan2>:р
<an.sg>:б[sg]
<an.pl>:й[pl]

# -------------------------------------------------------------------------
# CLITICS
# -------------------------------------------------------------------------

LEXICON clitics
<wh>:>ди
<wh>:>кIо
<wh>:>гъи
<q>:>диле
<q>:>кIоле
<q>:>ле
<q.pst>:>гъиро
<rep>:>лъоди
<rep>:>гъоди
<quot>:>гъоду
<quot>:>ду
<int>:>гу
<advz>:>гужа
<add>:>ло
<coll>:>ло
<com>:>лой
<equ>:>лъий
<sbd>:>лоду
<futimp>:>хъо
<attr>:>сси

# -------------------------------------------------------------------------
# INFLECTION
# -------------------------------------------------------------------------

PATTERNS
NumeralsAbs OblStem[m] CasesM
NumeralsAbs OblStem[-m] (Case[-v_epentetic,-m] | (Epent[sg] Case[v_epentetic,-m]))

AdjectiveMSg [<ADJ>:] (OblStem[m] CasesM)?
AdjectiveMPl [<ADJ>:] AdjectivePlural (Epent[m] Case[-f])?
AdjectiveSgConsonantal [<ADJ>:] (Case[-v_epentetic,-m] | (Epent[sg] Case[v_epentetic,-m]))?
AdjectiveSgVocalic [<ADJ>:] (OblStem[-m] (Case[-v_epentetic,-m] | (Epent[sg] Case[v_epentetic,-m])))?
AdjectivePl [<ADJ>:] AdjectivePlural (Epent[pl] Case[-m])?

PATTERN AdjectiveMSg
Adjectives[-initial_aggr,-final_aggr]
(agrCL[m] Adjectives[initial_aggr,-plural_slot])
(Adjectives[final_aggr] agrCL[m])
agrCL[-pl,m] [:у] Adjectives[initial_aggr,plural_slot]

PATTERN AdjectiveMPl
Adjectives[-initial_aggr,-final_aggr]
agrCL[m] Adjectives[initial_aggr,-plural_slot]
Adjectives[final_aggr] agrCL[m]
agrCL[-sg,m] [:о] Adjectives[initial_aggr,plural_slot]

PATTERN AdjectiveSgConsonantal
Adjectives[-initial_aggr,-final_aggr,сonsonantal]
agrCL[-m,-pl] Adjectives[initial_aggr,сonsonantal,-plural_slot]
Adjectives[final_aggr] agrCL[-m,-pl]
agrCL[-m,-pl] [:и] Adjectives[initial_aggr,plural_slot,сonsonantal]

PATTERN AdjectiveSgVocalic
Adjectives[-initial_aggr,-final_aggr,-сonsonantal]
agrCL[-m,-pl] Adjectives[initial_aggr,-сonsonantal,-plural_slot]
agrCL[-m,-pl] [:и] Adjectives[initial_aggr,plural_slot,-сonsonantal]

PATTERN AdjectivePl
Adjectives[-initial_aggr,-final_aggr]
agrCL[-m,-sg] Adjectives[initial_aggr,-plural_slot]
Adjectives[final_aggr] agrCL[-m,-sg]
agrCL[-m,-sg] [:о] Adjectives[initial_aggr,plural_slot,-сonsonantal]

LEXICON Epent
<epent.m>:>у[m]
<epent.sg>:>лъи[sg]
<epent.pl>:>и[pl]

LEXICON OblStem
<obl.m>:>ш[m]
<obl>:>л

PATTERN CasesM
Epent[m] Case[v_epentetic,-f]
Case[-v_epentetic,-f]
Case[ess] Elative

LEXICON Case
<erg>:>ди
<dat>:>лъу[f]
<dat>:>й[v_epentetic]
<gen>:>лIи[f]
<m><gen>:>в[m,v_epentetic]
<f><gen>:>й[m,v_epentetic]
<inan1><gen>:>б[m,v_epentetic]
<inan2><gen>:>р[m,v_epentetic]
<an.sg><gen>:б[m,v_epentetic]
<an.pl><gen>:й[m,v_epentetic]

<m><aff>:>во[v_epentetic]
<f><aff>:>йо[v_epentetic]
<inan1><aff>:>бо[v_epentetic]
<inan2><aff>:>ро[v_epentetic]
<an.sg><aff>:бо[v_epentetic]
<an.pl><aff>:йо[v_epentetic]

<sub><ess>:>кьи[ess]
<sub><lat>:>кьи
<super><ess>:>ъа[ess]
<super><lat>:>ъо
<in><ess>:>ла[ess]
<super><lat>:>ди
<inter><ess>:>лIи[ess]
<inter><lat>:>лIи
<add><ess>:>ха[ess]
<add><lat>:>хо
<apud><ess>:>хъи[ess]
<apud><lat>:>хъи
<cont><ess>:>чIу[ess]
<contr><lat>:>чIу

LEXICON Elative
<elat>:кку


# -------------------------------------------------------------------------
# NUMERALS
# -------------------------------------------------------------------------

PATTERNS
NumeralsAbs
NumeralsNotAbs
Numerals[one] [<NUM>:] agrCL[-pl]                                               ## 1

PATTERN NumeralsAbs
(Numerals[-one,-four] | NumeralFour) [<NUM>:] NumearalMarker                    ## rest numerals
NumeralMultiplier (NumeralCrown | (Numerals[base] [<NUM>:])) NumearalMarker     ## crowns, 100, 1000

PATTERN NumeralsNotAbs
Numerals[ten] [<NUM>:] NumearalTeenLinker                                       ## ten in 11-19
NumeralMultiplier NumeralCrown NumearalLinker                                   ## crowns
NumeralMultiplier? Numerals[base] [<NUM>:]                                      ## 100, 1000

PATTERN NumeralMultiplier
(Numerals[multiplier] | NumeralFour) [<NUM>:]

PATTERN NumeralFour
agrCL[-pl] Numerals[four]

LEXICON Numerals
се[one]                                                                         # один; one
чIе[multiplier]                                                                 # два; two
лъоб[multiplier]                                                                # три; three
оъо[four]                                                                       # четыре; four
иᵸшду[multiplier]                                                               # пять; five
ойлIи[multiplier]                                                               # шесть; six
гьокьу[multiplier]                                                              # семь; seven
бейкьи[multiplier]                                                              # восемь; eight
гьочIо[multiplier]                                                              # девять; nine
гьоцIо[ten]                                                                     # десять; ten
бешену:>бешену[base]                                                            # сто; hundred
озуру:>озуру[base]                                                              # тысяча; thousand

LEXICON NumearalMarker
<num>:>гу

LEXICON NumearalTeenLinker
<teen_link>:>лъий

LEXICON NumearalLinker
<linker>:>лой

LEXICON NumeralCrown
<crown>:>цIол

